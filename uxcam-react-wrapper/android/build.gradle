
buildscript {
    ext.safeExtGet = {prop, fallback ->
        rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.2'
    }
}

def isNewArchitectureEnabled() {
    if (project.hasProperty("uxcamNewArchEnabled")) {
        return project.uxcamNewArchEnabled == "true"
    }
    return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

apply plugin: 'com.android.library'
if (isNewArchitectureEnabled()) {
    apply plugin: 'com.facebook.react'
}

android {
    namespace "com.uxcam"
    compileSdkVersion safeExtGet('compileSdkVersion', 33)
    buildToolsVersion safeExtGet('buildToolsVersion', "33.0.1")

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 21)
        targetSdkVersion safeExtGet('targetSdkVersion', 33)
        versionCode 1
        versionName "1.0"
        buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
    }
    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            if (isNewArchitectureEnabled()) {
                java.srcDirs += ['src/newarch']
            } else {
                java.srcDirs += ['src/oldarch']
            }
        }
    }
}

repositories {
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$projectDir/../node_modules/react-native/android"
    }
    mavenCentral()
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://sdk.uxcam.com/android/'
        }
    }
}

dependencies {
    implementation("com.facebook.react:react-android")
    implementation 'com.uxcam:uxcam:3.8.10'
}

if (isNewArchitectureEnabled()) {
     react {
         jsRootDir = file("../src/")
         libraryName = "RNUxcam"
         codegenJavaPackageName = "com.uxcam"
    }
}
